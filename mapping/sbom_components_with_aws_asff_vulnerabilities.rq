PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX cybersec: <https://vocab.siemens-energy.com/cybersec/>
PREFIX component: <https://vocab.siemens-energy.com/cybersec/component/>
PREFIX nvd: <https://ontology.siemens-energy.com/nvd/>

INSERT {
    GRAPH <urn:links:sbom:aws:asff> {
    	?library nvd:hasVulnerability ?vulnerability  . 
    }
} 
WHERE {
	SELECT DISTINCT ?library ?vulnerability WHERE {
    	GRAPH <urn:aws-asff> {
    		?aws_asff_security_finding rdf:type cybersec:SecurityFinding .
    		?aws_asff_security_finding cybersec:hasVulnerability ?vulnerability .
    		?vulnerability cybersec:vulnerablePackageName ?package_name . # e.g. "ansi-regex"
    	}
    	GRAPH <urn:sbom-aws-asff>{
        	?library rdf:type component:library .
        	?library rdfs:label ?package_name . # e.g. "ansi-regex"
    	}
	} 
}